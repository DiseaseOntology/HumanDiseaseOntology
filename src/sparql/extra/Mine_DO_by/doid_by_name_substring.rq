# Get xrefs for DOIDs with matches in their name
# Example: All ICD9, ICD10, & SNOMED xrefs for DOIDs with 'disorder' in their
# name (case insensitive).
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX oboInOwl: <http://www.geneontology.org/formats/oboInOwl#>
PREFIX DOID: <http://purl.obolibrary.org/obo/DOID_>

SELECT ?id ?label ?xref
WHERE {
	?class oboInOwl:id ?id ;
		rdfs:label ?label ;
		oboInOwl:hasDbXref ?xref  .
	FILTER( REGEX(?xref, "^(ICD9|ICD10|SNOMED)") )
	FILTER NOT EXISTS { ?class owl:deprecated true }
	FILTER( CONTAINS( lcase(?label), "disorder" ) )
}