# Identify xrefs/exact mappings annotated to multiple diseases

PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX oboInOwl: <http://www.geneontology.org/formats/oboInOwl#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>

SELECT DISTINCT ?entity ?property ?value
WHERE {
    {
        SELECT ?value
        WHERE {
            VALUES ?property {
                oboInOwl:hasDbXref
                skos:exactMatch
            }
            ?entity a owl:Class ;
                skos:exactMatch ?value .
            FILTER(
                STRSTARTS(STR(?entity), "http://purl.obolibrary.org/obo/DOID_")
            )
            FILTER NOT EXISTS { ?entity owl:deprecated ?any }
        }
        GROUP BY ?value
        HAVING (COUNT(DISTINCT ?entity) > 1)
    } 
    ?entity a owl:Class ;
        ?property ?value .
    FILTER(STRSTARTS(STR(?entity), "http://purl.obolibrary.org/obo/DOID_"))
    FILTER NOT EXISTS { ?entity owl:deprecated ?any }
}
ORDER BY ?value ?entity ?property