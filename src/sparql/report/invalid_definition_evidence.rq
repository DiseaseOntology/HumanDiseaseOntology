# identifies invalid predicates used for ECO codes and where something has
# been used instead of an expected ECO code

PREFIX dc: <http://purl.org/dc/elements/1.1/>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX obo: <http://purl.obolibrary.org/obo/>

SELECT DISTINCT ?entity ?property ?value
WHERE {
	?entity a owl:Class ;
		obo:IAO_0000115 ?definition .
	?blank owl:annotatedTarget ?definition ;
		?property ?value .
	FILTER(
		(CONTAINS(STR(?value), "ECO_") && ?property != dc:type) ||
		(!CONTAINS(STR(?value), "ECO_") && ?property = dc:type)
	)

	FILTER NOT EXISTS { ?entity owl:deprecated true }
}
