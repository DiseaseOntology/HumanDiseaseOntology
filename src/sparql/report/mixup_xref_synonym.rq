# Ensure that synonyms and xrefs are not mixed up

PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX oboInOwl: <http://www.geneontology.org/formats/oboInOwl#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>

SELECT DISTINCT ?entity ?property ?value
WHERE {
	{
		VALUES ?property {
			oboInOwl:hasDbXref
			skos:exactMatch
			skos:broadMatch
			skos:narrowMatch
			skos:relatedMatch
		}
		?entity a owl:Class ;
			?property ?value .
		FILTER(!CONTAINS(?value, ":"))
		FILTER NOT EXISTS { ?entity owl:deprecated ?any }
	}
	UNION
	{
		VALUES ?property {
			oboInOwl:hasExactSynonym
			oboInOwl:hasNarrowSynonym
			oboInOwl:hasBroadSynonym
			oboInOwl:hasRelatedSynonym
		}
		?entity a owl:Class ;
			?property ?value .
		FILTER(CONTAINS(?value, ":"))
		FILTER NOT EXISTS { ?entity owl:deprecated ?any }
	}
}