# return all entities with more than one comment
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>

SELECT DISTINCT ?entity ?property ?value
WHERE {
  {
    SELECT ?entity ?property (COUNT(?val) AS ?count)
	  (GROUP_CONCAT(DISTINCT ?val;separator="    |    ") AS ?comments_delimited)
    WHERE {
      VALUES ?property { rdfs:comment }
      ?entity ?property ?val .
      FILTER NOT EXISTS { ?entity owl:deprecated true }
    } GROUP BY ?entity ?property
    HAVING (?count > 1)
  }

  BIND(CONCAT("comments (", str(?count), "): ", ?comments_delimited) AS ?value)
}