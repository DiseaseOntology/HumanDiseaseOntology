# identify all disease terms with GARD or ORDO xref(s) that are not in
#	DO_rare_slim

PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX oboInOwl: <http://www.geneontology.org/formats/oboInOwl#>
PREFIX doid: <http://purl.obolibrary.org/obo/doid#>

SELECT DISTINCT ?class
WHERE {
    ?class a owl:Class ;
        oboInOwl:hasDbXref ?xref .
    FILTER(REGEX(str(?xref), "GARD|ORDO"))
    FILTER NOT EXISTS { ?class oboInOwl:inSubset doid:DO_rare_slim . }
    FILTER NOT EXISTS { ?class owl:deprecated ?any . }
}
