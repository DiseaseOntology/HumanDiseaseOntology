# Ensure that no unexpected prefixes are used for xrefs or skos mappings exist
# (all OMIM xrefs should use MIM as prefix)

PREFIX oboInOwl: <http://www.geneontology.org/formats/oboInOwl#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>

SELECT ?entity ?mapping_type ?mapping
WHERE {
	VALUES ?mapping_type {
		oboInOwl:hasDbXref
		skos:exactMatch skos:narrowMatch skos:broadMatch skos:relatedMatch
		skos:closeMatch
	}
	?entity ?mapping_type ?mapping .
	FILTER(CONTAINS(str(?entity), "DOID_"))
	FILTER(
		!REGEX(
			?mapping,
			 "^(EFO|GARD|ICD(9|10)CM|ICD11|ICDO|KEGG|MEDDRA|MESH|MIM|NCI|ORDO|UMLS_CUI|SNOMEDCT_US_202[0-9]_[0-9]{2}_[0-9]{2}):"
		)
	)
}