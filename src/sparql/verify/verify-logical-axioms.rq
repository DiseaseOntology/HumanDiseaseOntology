# Ensure that all logical defs begin with a DOID term

PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>

SELECT ?iri ?axiom_first_term
WHERE {
  {
    ?iri a owl:Class .
    FILTER (STRSTARTS(STR(?iri), "http://purl.obolibrary.org/obo/DOID_"))
    ?iri rdfs:subClassOf ?axiom_first_term .
    FILTER (!isBlank(?axiom_first_term))
    FILTER (!STRSTARTS(STR(?axiom_first_term), "http://purl.obolibrary.org/obo/DOID_"))
  } UNION {
    ?iri a owl:Class .
    FILTER (STRSTARTS(STR(?iri), "http://purl.obolibrary.org/obo/DOID_"))
    ?iri owl:equivalentClass/owl:intersectionOf/rdf:first ?axiom_first_term .
    FILTER (!STRSTARTS(STR(?axiom_first_term), "http://purl.obolibrary.org/obo/DOID_"))
  }
}